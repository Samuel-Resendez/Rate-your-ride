usePlugin 'java'

// Various ways to access the main source set
println sourceSets.main.classesDir
println sourceSets['main'].classesDir
sourceSets {
    println main.classesDir
}
sourceSets {
    main {
        println classesDir
    }
}

// Iterate over the source sets
sourceSets.each {SourceSet set ->
    println set.name
}

sourceSets {
    intTest
}

repositories {
    mavenCentral()
}

configurations {
    intTestCompile { extendsFrom compile }
    intTestRuntime { extendsFrom intTestCompile, runtime }
}

sourceSets {
    intTest {
        compileClasspath = sourceSets.main.classes + configurations.intTestCompile
        runtimeClasspath = classes + sourceSets.main.classes + configurations.intTestRuntime
    }
}

task intTestJar(type: Jar) {
    from sourceSets.intTest.classes
}

task intTestJavadoc(type: Javadoc) {
    source sourceSets.intTest.allJava
}

task intTest(type: Test) {
    testClassesDir = sourceSets.intTest.classesDir
    classpath = sourceSets.intTest.runtimeClasspath
}
